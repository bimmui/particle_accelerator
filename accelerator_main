/**************************************************************
 *
 *                     beambreak_manager.ino
 *
 *     Assignment: Comp Phys Final Project (Phys 68)
 *     Author(s):  Daniel Opara
 *     Date:       7/23/2023
 *
 *     Overview: Driver code for the particle acclerator. Activates electromagnets
 *                to accelerate steel ball based on the photogate it passes 
 *
 *
 **************************************************************/



#include "Queue.h"

// - - - - - - - - - - - - - - - - - - - - - - - - - - -
//   Defining digital pins and constants
// - - - - - - - - - - - - - - - - - - - - - - - - - - -

#define LEDPIN 13 // Pin 13: Arduino has an LED connected on pin 13

// Mosfet pins correspond to gates with respect to its number
#define MP1 37
#define MP2 31
#define MP3 45
#define MP4 51

// Beambreak sensors, separeated into pairs as inidicated
// Gate #1
#define SENSORPIN1 52
#define SENSORPIN2 53

// Gate #2
#define SENSORPIN3 26
#define SENSORPIN4 33

// Gate #3
#define SENSORPIN5 48
#define SENSORPIN6 49

// Gate #4
#define SENSORPIN7 46
#define SENSORPIN8 47

// the radius of the particle acclerator as a whole
const float ACCELERATOR_RADIUS = 22.5; // centimeters
// angular separation of the beams within the photogate
const float BEAM_ANGULAR_DISTANCE = 0.15708; // radians

const float PHOTOGATE_SOLENOID_DISTANCE = 70.6858; // millimeters
const float PHOTOGATE_SOLENOID_CENTER_DISTANCE = 117.8097; // millimeters



// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
//   Necessary data structures
// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

typedef struct photogateControl {
   int sensorpin1;
   int sensorpin2;
   int electromagnet;
} photogateControl;

Queue<photogateControl> sensor_queue(4);



// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
//   Variables that will change throughout the program's lifetime
// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

// defines the first set of sensors the ball encounters
int curr_sensor1; // entering photogate
int curr_sensorState1 = 0, curr_lastState1 = 0;

// defines the second set of sensors the ball encounters
int curr_sensor2; // exiting photogate
int curr_sensorState2 = 0, curr_lastState2 = 0;

// defines the electromagnet that will be turned on and off by its
// respective photogate
int curr_electromagnet;

// used for calcaluting the time at which the ball will first 
// enter the magnetic field and when it will be at the center of it
unsigned long first_timestamp = 0, second_timestamp = 0; // milliseconds
unsigned long time_interval = 0;
float velocity = 0; // millimeters per millisecond
float angular_velocity = 0; // degrees per millisecond
bool is_velocity_calculated = false;

// the times at which the ball will first enter the mag field
// and when it will be in the center of it
float t3 = 0, t4 = 0; // milliseconds



// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
//   Helper functions
// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

/*
* calculateVelocity
* Parameters: The recorded timestamps for each sensor in a compartment
* Purpose: Calculates velocity of the ball
* Returns: Nothing
* Effects: Modifies the value stored insider the "velocity" variable
*/
bool calculateVelocity() {
  time_interval = second_timestamp - first_timestamp;
  angular_velocity = BEAM_ANGULAR_DISTANCE / (float) time_interval;
  velocity = ACCELERATOR_RADIUS * angular_velocity;
  return true;
}

/*
* calculateElectromagnetActivationTimes
* Parameters: The current velocity of the ball
* Purpose: Calculates the time at which the program should turn on the elctromagnet
* Returns: Nothing
* Effects: Modifies the value stored inside "t3" and "t4"
*/
void calculateElectromagnetActivationTimes() {
  t3 = (PHOTOGATE_SOLENOID_DISTANCE / velocity);
  t4 = (PHOTOGATE_SOLENOID_CENTER_DISTANCE / velocity);
}

/*
* checkTime
* Parameters: None
* Purpose: Calculates the time at which the program should turn on the elctromagnet
* Returns: Nothing
* Effects: Modifies the value stored inside "t3" and "t4"
*/
float checkTime() {
  return millis() - second_timestamp;
}

/*
* activateElectromagnet
* Parameters: None
* Purpose: Activates an electromagnet based on time interval from
*             second_timestamp to now  
* Returns: Nothing
* Effects: Once the time interval from second_timestamp to now is 
*             equal to t3, the electromagnet corresponding to the 
*             pair of beam break sensors is activated
*/
bool activateElectromagnet(int mosfet_pin) {
  if (checkTime() < t3) {
    return false;
  } else {
    digitalWrite(mosfet_pin, HIGH);
  }
  return true;
}

/*
* deactivateElectromagnet
* Parameters: None
* Purpose: Deactivates an electromagnet based on time interval from
*             second_timestamp to now  
* Returns: Nothing
* Effects: Once the time interval from second_timestamp to now is 
*             equal to t4, the electromagnet corresponding to the 
*             pair of beam break sensors is deactivated
*/
bool deactivateElectromagnet(int mosfet_pin) {
  if (checkTime() < t4) {
    return true;
  } else {
    digitalWrite(mosfet_pin, LOW);
  }
  return false;
}





/*************************************************************************/
/*                              MAIN PROGRAM                             */
/*************************************************************************/

void setup() {
  Serial.begin(9600);

  Serial.print("Setting up pin connections and sensor data...");
  // initialize the LED pin as an output:
  pinMode(LEDPIN, OUTPUT);    

  // set the mosfet pins as output
  pinMode(MP1, OUTPUT);
  pinMode(MP2, OUTPUT);
  pinMode(MP3, OUTPUT);
  pinMode(MP4, OUTPUT);

  // initialize the sensor pin as an input:
  // gate #1
  pinMode(SENSORPIN1, INPUT);     
  digitalWrite(SENSORPIN1, HIGH); // turn on the pullup
  pinMode(SENSORPIN2, INPUT);     
  digitalWrite(SENSORPIN2, HIGH); // turn on the pullup

  // gate #2
  pinMode(SENSORPIN3, INPUT);     
  digitalWrite(SENSORPIN3, HIGH); // turn on the pullup
  pinMode(SENSORPIN4, INPUT);     
  digitalWrite(SENSORPIN4, HIGH); // turn on the pullup
  
  // gate #3
  pinMode(SENSORPIN5, INPUT);     
  digitalWrite(SENSORPIN5, HIGH); // turn on the pullup
  pinMode(SENSORPIN6, INPUT);     
  digitalWrite(SENSORPIN6, HIGH); // turn on the pullup

  // gate #4
  pinMode(SENSORPIN7, INPUT);     
  digitalWrite(SENSORPIN7, HIGH); // turn on the pullup
  pinMode(SENSORPIN8, INPUT);     
  digitalWrite(SENSORPIN8, HIGH); // turn on the pullup
  Serial.println("Complete!");

  Serial.print("Setting up sensors...");

  sensor_queue.push(photogateControl{SENSORPIN1, SENSORPIN2, MP1});
  sensor_queue.push(photogateControl{SENSORPIN3, SENSORPIN4, MP2});
  sensor_queue.push(photogateControl{SENSORPIN5, SENSORPIN6, MP3});
  sensor_queue.push(photogateControl{SENSORPIN7, SENSORPIN8, MP4});

  photogateControl temp = sensor_queue.pop();
  curr_sensor1 = temp.sensorpin1;
  curr_sensor2 = temp.sensorpin2;
  curr_electromagnet = temp.electromagnet;
  Serial.print("Complete!");
  Serial.println("Program set up complete! Beginning program...");
}

void loop(){
  // read the state of the pushbutton value:
  curr_sensorState1 = digitalRead(curr_sensor1);
  curr_sensorState2 = digitalRead(curr_sensor2);
  
  if (!curr_sensorState1 && curr_lastState1) {
    first_timestamp = millis();
  }
  curr_lastState1 = curr_sensorState1;
  
  if (!curr_sensorState2 && curr_lastState2) {
    second_timestamp = millis();
    is_velocity_calculated = calculateVelocity();
  }
  curr_lastState2 = curr_sensorState2;

  if (is_velocity_calculated == true) {    
    // pushing the current sensors to the back of the queue
    // and grabbing the next set of sensor to monitor
    sensor_queue.push(photogateControl{curr_sensor1, curr_sensor2, curr_electromagnet});
    photogateControl temp = sensor_queue.pop();
    curr_sensor1 = temp.sensorpin1;
    curr_sensor2 = temp.sensorpin2;

    // reseting the sensor state variables to prep for the next pair of beams
    curr_sensorState1 = 0;
    curr_lastState1 = 0;
    curr_sensorState2 = 0;
    curr_lastState2 = 0;

    is_velocity_calculated = false;
    
    bool activated_electromagnet = false;
    bool deactivated_electromagnet = true;
    // setting this timestamp to more recent time to calculate t3 and t4
    second_timestamp = millis();
    calculateElectromagnetActivationTimes();
    while (activated_electromagnet != true) {
      activated_electromagnet = activateElectromagnet(curr_electromagnet);
    }
    second_timestamp = millis();
    while (deactivated_electromagnet != false) {
      deactivated_electromagnet = deactivateElectromagnet(curr_electromagnet); 
    }

    // sets the next electromagnet to be activated
    curr_electromagnet = temp.electromagnet;
  }
}
